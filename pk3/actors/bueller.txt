/*

PILLOW'S SHAMELESSLY BASED ON KEKSDOSE'S PARTICLE EFFECT BUILDING METHOD!

Use as you wish, as long as you credit fellas below.

Puffs code made by PillowBlaster, graphics by DukeNukem. Tweaked severely by an advices of KeksDose.
*/

//Bueller... Bueller... Bueller... Bueller... Bueller... Bueller...
ACTOR BuellerPuff replaces BulletPuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+ALLOWPARTICLES
	Mass 5
    Decal "BulletChip"
	States
	{
	Spawn:
		TNT1 AAA 0 A_SpawnItemEx("BuellerPuff_Main")
		TNT1 AA 0 A_SpawnItemEx("BuellerPuff_Flash")
		TNT1 A 0 A_SpawnItemEx("BuellerPuff_Spark1")
		TNT1 A 0 A_SpawnItemEx("BuellerPuff_Spark2")
		TNT1 AAA 1 A_SpawnItemEx("BuellerPuff_Smoke",0,0,0,frandom(0.15,0.25),0,frandom(-0.1,0.45))
		Stop
	}
}

ACTOR BuellerPuff_Flash
{
	+NOINTERACTION
	+FORCEXYBILLBOARD
	Alpha .425
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(FRandom(0.085,0.095))
		TNT1 A 0 A_Jump(256,1,2,3,4)
		2PUF ABCD 0 A_Jump(256,"Idle")
	Idle:
		"####" "#" 1 Bright A_FadeOut(0.125)
		Wait
		//"####" "#" 0 A_SetScale(ScaleX + FRandom(0.04,0.065))
		//"####" "#" 0 A_JumpIf(ScaleX>0.0,"Idle")
		//Stop
	}
}

ACTOR BuellerPuff_Main : BuellerPuff_Flash
{
	RenderStyle Add
	Alpha 0.99
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(FRandom(0.05,0.065))
		TNT1 A 0 A_Jump(256,1,2,3,4)
		1PUF ABCD 0 A_Jump(256,"Idle")
	Idle:
		"####" "#" 1 Bright A_Jump(192,"FadeOut")
		Wait
	FadeOut:
		"####" "#" 1 Bright A_FadeOut(0.2)
		"####" "#" 0 A_SetScale(ScaleX + FRandom(0.005,0.01))
		Loop
	}
}

ACTOR BuellerPuff_Spark1 : BuellerPuff_Main
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(FRandom(0.045,0.05))
		TNT1 A 0 A_Jump(256,1,2,3,4)
		1SPK ABCD 0 A_Jump(256,"Idle")
	FadeOut:
		"####" "#" 1 Bright A_FadeOut(0.2)
		"####" "#" 0 A_SetScale(ScaleX + FRandom(0.005,0.0075))
		Loop
	}
}

ACTOR BuellerPuff_Spark2 : BuellerPuff_Spark1
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SetScale(FRandom(0.045,0.05))
		TNT1 A 0 A_Jump(256,1,2,3,4)
		2SPK ABCD 0 A_Jump(256,"Idle")
		Stop
	}
}

ACTOR BuellerPuff_Smoke : BuellerPuff_Flash
{
	Alpha 0.0
	RenderStyle Translucent
	States
	{
	Spawn:
	    TNT1 A 3 NODELAY A_SetScale(FRandom(0.01, 0.015))
		TNT1 A 0 A_Jump(256,1,2,3,4)
		DYMI ABCD 0 A_Jump(256,"FadeIn")
	FadeIn:
		"####" "#" 1 A_FadeIn(FRandom(0.06,0.08))
		"####" "#" 0 A_JumpIf(Alpha>=0.25,1)
		Loop
		"####" "#" 5 A_Jump(200,"FadeOut")
		Wait
	FadeOut:
		"####" "#" 1 A_FadeOut(FRandom(0.01,0.02))
		"####" "#" 0 A_SetScale(ScaleX + FRandom(0.015,0.005))
		Loop
	}
}