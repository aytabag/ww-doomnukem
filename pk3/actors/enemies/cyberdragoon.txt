// CyberDragoon.

ACTOR CyberDragoon : Cyberdemon replaces Cyberdemon
{
	var int user_locktimer;
	States
	{
	See:
		CYBR A 3 A_Hoof
		CYBR ABB 3 A_Chase
		CYBR C 3 A_Metal
		CYBR CDD 3 A_Chase
		Loop
	Missile:
		CYBR E 0 A_SetUserVar("user_locktimer",35)
		CYBR E 1 A_FaceTarget
	Missile.Loop:
		CYBR E 0 A_JumpIf(user_locktimer==0, "Missile.Fire")
		CYBR E 0 A_SetUserVar("user_locktimer",user_locktimer-1)
		CYBR E 0 A_VileTarget("DragoonLockOn")
		CYBR E 0 A_JumpIfTargetInLOS(2, 90)
		CYBR E 1 A_FaceTarget
		Loop
		CYBR E 1
		Goto Missile.Loop
	Missile.Fire:
		CYBR E 0 A_PlaySound("Weapons/DragoonRocket/Fire", CHAN_WEAPON,1.0,0,ATTN_NONE)
		CYBR F 6 BRIGHT A_CustomMissile("DragoonRocket", 40, -8, 0, CMF_AIMDIRECTION)
		Goto See
	}
}

actor DragoonRocket
{
	scale 0.5
	Radius 11
	Height 8
	Speed 75
	Damage (50)
	Projectile
	+RANDOMIZE
	MissileHeight 0
	Decal Scorch
	States
	{
	Spawn:
		GLNB A 1 A_SpawnItemEx("DN_MissileTrail",-15,0,0,0,0,0,0,SXF_CLIENTSIDE,0) // A_CustomMissile("RocketFlameSpawner",0,0,0,5)
		loop
	Death:
		TNT1 A 0 A_PlaySound("Weapons/DragoonRocket/Explode", CHAN_BODY, 2.0, 0, 0.25)
		TNT1 A 1 A_Explode(96,128)
		TNT1 A 0 A_SpawnItemEx("DN_Blastage",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
		stop
	}
}

ACTOR DragoonLockOn
{
	RenderStyle Add
	Alpha 1
	Scale 0.25
	+NOGRAVITY
	+NOBLOCKMAP
	States
	{
	Spawn:
		BAL1 A 0 NoDelay A_Fire(40)
		BAL1 A 0 A_PlaySound("misc/chat2")
	SpawnLoop:
		BAL1 A 1 A_FadeOut(0.1,1)
		Loop
	}
}