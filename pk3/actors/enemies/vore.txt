
// Vore
//Decorate: Ghastly_dragon
//GLDefs: Ghastly_dragon
//Sounds: Id Software
//Sprites: Nmn (sprite set), Vader (projectile)
//Idea Base: Vore from Quake 1

Actor Vore replaces Archvile
{
	Health 700
	Speed 10
	Height 56
	Mass 1000
	RadiusDamageFactor 0.5
	Monster
	+FLOORCLIP
	+MISSILEMORE
	+NOTARGET //Intended so they don't infight with other Vores. If a DontInfightSpecies gets in, use that
	Obituary "%o couldn't escape a Vore."
	SeeSound "Vore/Sight"
	ActiveSound "Vore/Active"
	PainSound "Vore/Pain"
	DeathSound "Vore/Death"
	States
	{
	Spawn:
		VORE D 10 A_LookEx(0,0,0,0,0,"SeeFirst")
		Loop
	SeeFirst:
		VORE A 3 A_Chase
		VORE A 0 A_SetSpeed(10.0/3.0)
		Goto See+4
	See:
		VORE A 0 A_SetSpeed(10.0/3.0)
		VORE A 1 A_Chase
		VORE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		VORE A 1 A_Chase
		VORE AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		VORE B 1 A_Chase
		VORE BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		VORE B 4
		VORE C 1 A_Chase
		VORE CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		VORE C 1 A_Chase
		VORE CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		VORE D 1 A_Chase
		VORE DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		VORE D 4
		VORE E 1 A_Chase
		VORE EE 1 A_Chase("","",CHF_NOPLAYACTIVE)
		VORE E 1 A_Chase
		VORE EE 1 A_Chase("","",CHF_NOPLAYACTIVE)
		VORE F 1 A_Chase
		VORE FF 1 A_Chase("","",CHF_NOPLAYACTIVE)
		VORE F 4
		Loop
	Melee:
	Missile:
		VORE G 0 A_PlaySound("Vore/Attack")
		VORE GHII 4 Bright A_FaceTarget
		VORE J 8 Bright A_CustomMissile("VoreFirepod",32)
		VORE JJ 4 Bright A_FaceTarget
		Goto See
	Death:
		VORE L 6
		VORE M 6 A_Scream
		VORE N 6
		VORE O 6 A_NoBlocking
		VORE P 8
		VORE Q -1
		Stop
	Raise:
		VORE QPONML 6
		Goto See
	}
}

Actor VoreFirepod
{
	Radius 8
	Height 12
	Speed 15
	FastSpeed 30
	Health 25
	Damage (30)
	Mass 0x7fffffff
	Projectile
	+SEEKERMISSILE
	+DONTSEEKINVISIBLE
	+SHOOTABLE
	-NOBLOCKMAP
	+NOBLOOD
	RenderStyle Add
	Alpha 0.9
	SeeSound "Vore/Throw"
	DeathSound "Vore/Hit"
	Decal RevenantScorch
	States
	{
	Spawn:
		VOBA A 0 A_SpawnItemEx("VoreFirepodTrail",random(5,-5),0,random(5,-1))
		VOBA A 1 Bright A_SpawnItemEx("VoreFirepodTrail",random(5,-5),0,random(5,-1))
		VOBA A 0 A_SpawnItemEx("VoreFirepodTrail",random(5,-5),0,random(5,-1))
		VOBA A 1 Bright A_SpawnItemEx("VoreFirepodTrail",random(5,-5),0,random(5,-1))
		VOBA B 0 A_SpawnItemEx("VoreFirepodTrail",random(5,-5),0,random(5,-1))
		VOBA B 1 Bright A_SpawnItemEx("VoreFirepodTrail",random(5,-5),0,random(5,-1))
		VOBA B 0 A_SpawnItemEx("VoreFirepodTrail",random(5,-5),0,random(5,-1))
		VOBA B 1 Bright A_SpawnItemEx("VoreFirepodTrail",random(5,-5),0,random(5,-1))
		VOBA C 0 A_SpawnItemEx("VoreFirepodTrail",random(5,-5),0,random(5,-1))
		VOBA C 1 Bright A_SpawnItemEx("VoreFirepodTrail",random(5,-5),0,random(5,-1))
		VOBA C 0 A_SpawnItemEx("VoreFirepodTrail",random(5,-5),0,random(5,-1))
		VOBA C 1 Bright A_SpawnItemEx("VoreFirepodTrail",random(5,-5),0,random(5,-1))
		VOBA C 0 A_JumpIfTargetInLOS(1,150,1)
		Loop
		VOBA A 0 A_SpawnItemEx("VoreFirepodTrail",random(5,-5),0,random(5,-1),0,0,0,180)
		VOBA A 1 Bright A_SpawnItemEx("VoreFirepodTrail",random(5,-5),0,random(5,-1),0,0,0,180)
		VOBA A 0 A_SpawnItemEx("VoreFirepodTrail",random(5,-5),0,random(5,-1),0,0,0,180)
		VOBA A 1 Bright A_SpawnItemEx("VoreFirepodTrail",random(5,-5),0,random(5,-1),0,0,0,180)
		VOBA C 0 A_JumpIfTargetInLOS(1,150,1)
		Goto Spawn+4
		VOBA BBB 0 A_SeekerMissile(45,0.1,SMF_PRECISE)
		Goto Spawn+4
	Death:
		VOBA D 0 A_Explode(10,90)	//Part of the original game was that Vores
		VOBA DEFGHI 4 Bright				//can be hurt by their own projectiles
		Stop
	}
}

Actor VoreFirepodTrail
{
	Radius 1
	Height 1
	Projectile
	RenderStyle Add
	Alpha 0.9
	+NoClip
	States
	{
	Spawn:
	Death:
		TNT1 A 3
		VOBT ABCDE 3 Bright
		Stop
	}
}