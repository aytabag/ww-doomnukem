ACTOR ConflagSoul replaces LostSoul
{ 
	obituary "%o was made extra-crispy by a Conflagrated Soul."
	health 100
	radius 16
	height 32
	mass 400
	speed 5
	damage 5
	painchance 128
	renderstyle Add
	Alpha 0.4
	PainSound "soul/pain"
	DeathSound "soul/death"
	ActiveSound "soul/active"
	SeeSound "soul/see"
	MONSTER
	+FLOAT
	+NOGRAVITY
	+NOICEDEATH
	+MISSILEMORE
	+DONTFALL
	+NOBLOOD
	states
	{
	Spawn:
		ETHS ABCD 10 bright A_Look
		loop
	See:
		ETHS AABBCCDD 3 bright A_Chase 
		loop
	Missile:
		ETHS C 0 A_PlaySound("soul/attack1", CHAN_BODY)
		ETHS C 6 bright A_FaceTarget
		ETHS E 4 bright A_SkullAttack
		ETHS E 4 bright A_FaceTarget
		ETHS F 4 bright A_FaceTarget
		ETHS E 4 bright A_FaceTarget
		goto See
	Pain:
		ETHS G 3 bright
		ETHS G 3 bright A_Pain
		goto See
	Death:
		ETHS F 3 bright
		ETHS H 3 bright A_ScreamAndUnblock
		ETHS IJKLMNO 3 bright
		stop
	}
}

ACTOR ConflagBall
{
	Radius 6
	Height 8
	Speed 12
	Damage 4
	Projectile
	+RANDOMIZE
	RenderStyle "ADD"
	Alpha 0.75
	SeeSound "soul/attack2"
	DeathSound "imp/shotx"
	Decal DoomImpScorch
	States
	{
	Spawn: 
		SBAL ABC 4 BRIGHT A_SpawnItemEx("ConflagTrail", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
	Death:
		SBAL C 5 Bright
		SBAL DEFGH 4 BRIGHT
		Stop
	}
}


actor ConflagTrail
{
    Radius 1
    Height 1
    Speed 0
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.6
    +NOCLIP
    States
    {
    Spawn:
     SHTR ABCDEF 4 Bright
        Stop       
    }
}