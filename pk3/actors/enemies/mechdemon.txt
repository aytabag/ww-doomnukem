//Original Decorate: Carnevil
//GLDefs: Sandypaper (Brightmaps)
//Sounds: Id Software
//Sprites: Id Software
//Sprite Edit: Neoworm, Vader
//Idea Base: Skulltag's Blood Demon by Cyb

ACTOR MechDemon replaces Spectre
{
	//$Category Monsters
	Health 300
	Speed 6
	Height 56
	Radius 30
	Mass 400
	PainChance 180
	Monster
	+FLOORCLIP
	+STEALTH
	SeeSound "blooddemon/sight"
	PainSound "blooddemon/pain"
	DeathSound "blooddemon/death"
	ActiveSound "blooddemon/active"
	HitObituary "%o was chewed up and spat out by a Mech-demon."
	States
	{
	Spawn:
		SRG2 AB 10 A_Look
		Loop
	See:
		SRG2 A 0 A_SpawnItemEx("BloodDemonStepPuff")
		SRG2 A 0 A_PlaySound("blooddemon/walk")
		SRG2 A 1 A_Chase
		SRG2 A 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SRG2 A 1 A_Chase
		SRG2 A 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SRG2 B 1 A_Chase
		SRG2 B 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SRG2 B 1 A_Chase
		SRG2 B 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SRG2 A 0 A_SpawnItemEx("BloodDemonStepPuff")
		SRG2 C 0 A_PlaySound("blooddemon/walk")
		SRG2 C 1 A_Chase
		SRG2 C 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SRG2 C 1 A_Chase
		SRG2 C 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SRG2 D 1 A_Chase
		SRG2 D 1 A_Chase("","",CHF_NOPLAYACTIVE)
		SRG2 D 1 A_Chase
		SRG2 D 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Melee:
		SRG2 E 0 A_PlaySound("blooddemon/melee")
		SRG2 EF 8 A_FaceTarget
		SRG2 G 8 A_CustomMeleeAttack(random(1,8)*5)
		Goto See
	Pain:
		SRG2 H 2
		SRG2 H 2 A_Pain
		Goto See
	Death:
		SRG2 I 8
		SRG2 I 0 A_FaceTarget
		SRG2 J 0 A_SpawnItemEx("BloodDemonArm",10,0,32,0,8)
		SRG2 J 8 A_Scream
		SRG2 K 4
		SRG2 L 4 A_NoBlocking
		SRG2 M 4
		SRG2 N -1
		Stop
	Raise:
		SRG2 NMLKJI 5
		Goto See
	}
}

ACTOR BloodDemonArm
{
	Radius 8
	Height 8
	Speed 8
	BounceType Doom
	+DROPOFF
	+MISSILE
	States
	{
	Spawn:
		SG2A ABCDEFGH 2 A_SpawnItemEx("Blood",0,0,0,0,0,random(1,3),random(0,359))
		Loop
	Death:
		SG2A I -1
		Loop
	}
}

ACTOR BloodDemonStepPuff
{
	+NOTELEPORT
	States
	{
	Spawn:
		POOF AAAAA 0 A_SpawnItemEx("GunSmoke",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,SXF_NOCHECKPOSITION)
		Stop
	}
}