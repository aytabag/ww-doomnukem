ACTOR DNRifleSeq : Inventory{}

ACTOR DNRifle : DNWeapon replaces Chaingun
{
	Weapon.AmmoType "DNRifleAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "DNRifleAmmoPickup"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 2
	Weapon.SlotNumber 4
	Inventory.PickupSound "Weapons/Rifle/Click"
	Inventory.PickupMessage "You got the K&S R90-PCW Rifle! (slot 4)"
	Weapon.SelectionOrder 4000
	Tag "K&S R90-PCW"
	AttackSound "Weapons/Rifle/Fire"
	+Weapon.AMMO_CHECKBOTH
	+Weapon.NOALERT
	+Weapon.NOAUTOFIRE
	States
	{
	Spawn:
		RIFL A -1 Nodelay A_SetAngle(random(0,359))
		Stop
	Select:
	Ready:
		RIFR H 1 Offset(200,50) A_PlaySound("Weapons/Select2")
		RIFR I 1 Offset(175,49)
		RIFR J 1 Offset(140,47)
		RIFR K 1 Offset(110,45)
		RIFR L 1 Offset(65,43)
		RIFR M 1 Offset(50,41)
		RIFR N 1 Offset(40,39)
		RIFR O 1 Offset(32,38)
		RIFR P 1 Offset(28,37)
		RIFR Q 1 Offset(20,36)
		RIFR R 1 Offset(10,35)
		RIFR S 1 Offset(5,34)
		RIFG A 1 Offset(3,33)
	Ready2:
		RIFG A 0 A_JumpIfInventory("DNRifleSighting", 1, "ReadyZoom")
		RIFG A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_TakeInventory("PressingZoom")
		Loop
	ReadyZoom:
		RI2G A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_TakeInventory("PressingZoom")
		Goto Ready2
	Deselect:
		RI2G A 0 A_JumpIfInventory("DNRifleSighting", 1, "DeselectZoom")
		RIFG A 1 Offset(15,40)
		RIFG A 1 Offset(30,55)
		RIFG A 1 Offset(45,75)
		RIFG A 1 Offset(60,100)
		RIFG A 1 Offset(75,130)
		TNT1 A 10
		TNT1 A 0 A_Lower
		Wait
	DeselectZoom:
		RIFZ D 1 A_ZoomFactor(1.0)
		RIFZ C 1 Offset(0,35) A_TakeInventory("DNRifleSighting", 1)
		RIFZ B 1 Offset(0,32) A_SetCrosshair(0)
		RIFZ A 1 Offset(0,35)
		RIFG A 1 Offset(0,32)
		Goto Deselect
	Fire:
		RIFG A 0 A_JumpIfInventory("DNRifleSighting", 1, "FireZoom")
		TNT1 A 0 A_JumpIfInventory("DNRifleAmmo",1,1)
		Goto Dryfire
		RIFF A 1 BRIGHT A_FireBullets(2, 2, -1, 12, "BulletPuff")
		RIFF A 0 A_SpawnItemEx("RifleCasing", 12,-20,32, 8,random(-2,2),random(0,4), random(-55,-80), SXF_NOCHECKPOSITION)
		PISF A 0 A_FireCustomMissile("GunSmokeSpawner",0,0,0,8)
		RIFF A 0 A_AlertMonsters
		RIFF B 1 A_SetPitch(pitch - 0.5)
		RIFF C 1 A_JumpIfInventory("DNRifleSeq", 1, 4)
		RIFF C 0 A_GiveInventory("DNRifleSeq", 1)
		RIFF C 1 A_SetPitch(pitch - 0.25)
		RIFF C 0 A_Jump(256, 2)
		RIFF C 0 A_TakeInventory("DNRifleSeq", 1)
		RIFG A 1 A_SetPitch(pitch + 0.25)
		RIFG A 0 A_Refire
		Goto Ready2
	FireZoom:
		TNT1 A 0 A_JumpIfInventory("DNRifleAmmo",1,1)
		Goto DryfireZoom
		RI2F A 1 BRIGHT A_FireBullets(0, 0, 1, 12, "BulletPuff")
		RIFF A 0 A_SpawnItemEx("RifleCasing", 12,-20,32, 8,random(-2,2),random(0,4), random(-55,-80), SXF_NOCHECKPOSITION)
		PISF A 0 A_FireCustomMissile("GunSmokeSpawner",0,0,0,8)
		RI2F A 0 A_AlertMonsters
		RI2F B 0 A_SetAngle(angle + frandom(-1.0, 1.0))
		RI2F B 1 A_SetPitch(pitch - 1.0)
		RI2G A 1 A_JumpIfInventory("DNRifleSeq", 1, 4)
		RI2G A 0 A_GiveInventory("DNRifleSeq", 1)
		RI2G A 1 A_SetPitch(pitch - 0.5)
		RI2G A 0 A_Jump(256, 2)
		RI2G A 0 A_TakeInventory("DNRifleSeq", 1)
		RI2G A 1 A_SetPitch(pitch + 0.5)
		RI2G A 0 A_Refire
		Goto Ready2
	Zoom:
		TNT1 A 0 A_JumpIfInventory("PressingZoom",1,"AltHold")
		TNT1 A 0 A_GiveInventory("PressingZoom",1)
	Altfire:
		RIFG A 0 A_JumpIfInventory("DNRifleSighting", 1, "AltfireUnzoom")
	AltfireZoom:
		RIFZ A 1 Offset(0,35) A_ZoomFactor(2.0)
		RIFZ B 1 Offset(0,32) A_GiveInventory("DNRifleSighting", 1)
		RIFZ C 1 A_SetCrosshair(8)
		RIFZ D 1 Offset(0,35)
		RI2G A 1 Offset(0,33)
		Goto Ready2
	AltfireUnzoom:
		RIFZ D 1 A_ZoomFactor(1.0)
		RIFZ C 1 Offset(0,35) A_TakeInventory("DNRifleSighting", 1)
		RIFZ B 1 Offset(0,32) A_SetCrosshair(0)
		RIFZ A 1 Offset(0,35)
		RIFG A 1 Offset(0,32)
		Goto Ready2
	Althold:
		RIFG A 0 A_JumpIfInventory("DNRifleSighting", 1, "AltholdZoomed")
		RIFG A 1 A_WeaponReady(WRF_NOFIRE)
		RIFG A 0 A_Refire
		Goto Ready2
	AltholdZoomed:
		RI2G A 1 A_WeaponReady(WRF_NOFIRE)
		RI2G A 0 A_Refire
		Goto Ready2
	DryfireZoom:
		RI2F B 1 A_PlaySound("Weapons/Dryfire", 5)
		RI2G A 5
	ReloadZoom:
		RIFZ D 1 A_ZoomFactor(1.0)
		RIFZ C 1 Offset(0,35) A_TakeInventory("DNRifleSighting", 1)
		RIFZ B 1 Offset(0,32) A_SetCrosshair(0)
		RIFZ A 1 Offset(0,35) A_TakeInventory("PressingZoom", 999)
		RIFG A 1 Offset(0,32)
		Goto Reload
	Dryfire:
		RIFG A 0 A_JumpIfInventory("DNRifleSighting", 1, "DryfireZoom")
		RIFF B 1 A_PlaySound("Weapons/Dryfire", 5)
		RIFF C 1
		RIFG A 1
	Reload:
		RIFG A 0 A_ClearRefire
		RIFG A 0 A_JumpIfInventory("DNRifleSighting", 1, "ReloadZoom")
		RIFG A 0 A_JumpIfInventory("DNRifleAmmo", 20, 2)
		RIFG A 0 A_JumpIfInventory("DNRifleAmmoPickup", 1, "ProperReload")
		RIFG A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready2
	ProperReload:
		RIFR ABCDEF 1
		RIFR G 1 Offset(2,31) A_PlaySound("Weapons/Rifle/Out", 6)
		RIFR G 1 Offset(4,30) A_Jump(128,2)
		RIFR G 1 Offset(5,31)
		RIFR G 1 Offset(3,32)
		RIFR H 1 Offset(1,33)
		RIFR IJK 2
		TNT1 A 0 A_TakeInventory("DNRifleAmmoPickup", 1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("DNRifleAmmo", 20)
		RIFR L 2 A_PlaySound("Weapons/Rifle/In", 5)
		RIFR MNOPQ 2
		RIFR R 2 A_PlaySound("Weapons/Rifle/Click", 6)
		RIFR S 2
		Goto Ready2
	}
}

ACTOR DNRifleSighting : Inventory{}

ACTOR DNRifleAmmo : Ammo
{
	Inventory.Icon "RIFLA0"
	Inventory.MaxAmount 20
	+IGNORESKILL
}

ACTOR DNRifleAmmoPickup : Ammo replaces ClipBox
{
	Inventory.Icon "RIFLB0"
	Inventory.MaxAmount 10
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 20
	Inventory.PickupMessage "Picked up an assault rifle magazine."
	Inventory.PickupSound "Weapons/Rifle/In"
	Tag "6.8x40mm K&S"
	States
	{
	Spawn:
		RIFL B -1 Nodelay A_SetAngle(random(0,359))
		Stop
	}
}