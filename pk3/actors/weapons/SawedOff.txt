ACTOR DoubleShotgunLoad : Ammo
{
	+IGNORESKILL
	Inventory.MaxAmount 2
	Inventory.Icon "SGN2A0"
}

ACTOR DoubleShotgun : DNWeapon replaces Shotgun
{
	Inventory.PickupMessage "You got the Maxwell One-Two sawed-off! (slot 3)"
	Inventory.PickupSound "Weapons/Sawedoff/Close"
	Weapon.SlotNumber 3
	Weapon.AmmoType "DoubleShotgunLoad"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "NewShell"
	Weapon.AmmoUse2 0 // Altfire DOES take ammo - it just doesn't take it like this.
	Weapon.AmmoGive2 2
	Weapon.Kickback 1000
	Weapon.SelectionOrder 5000
	+Weapon.AMMO_CHECKBOTH
	+Weapon.NoAlert
	+Weapon.NoAutoFire
	Tag "Maxwell Labs 1-2"
	States
	{
	Spawn:
		SGN2 A -1 Nodelay A_SetAngle(random(0,359))
		Stop
	Deselect:
		SHT2 A 1 Offset(-3,35)
		SHT2 A 1 Offset(-7,39)
		SHT2 B 1 Offset(-12,45)
		SHT2 C 1 Offset(-20,52)
		SHT2 D 1 Offset(-35,60)
		SHT2 E 1 Offset(-50,69)
		SHT2 F 1 Offset(-70,79)
		TNT1 A 5
		TNT1 A 0 A_Lower
		Wait
	Select:
	Ready:
		SHT2 F 1 Offset(-70,79) A_PlaySound("Weapons/Select3")
		SHT2 E 1 Offset(-50,69)
		SHT2 D 1 Offset(-35,60)
		SHT2 C 1 Offset(-20,52)
		SHT2 B 1 Offset(-12,45)
		SHT2 A 1 Offset(-7,39)
		SHT2 A 1 Offset(-3,35)
	Ready2:
		SHT2 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("DoubleShotgunLoad",1,1)
		Goto DryFire
		SHT2 A 0 A_JumpIfInventory("DoubleShotgunLoad", 2, "FireRight")
		SHTA B 1 BRIGHT {
			A_StartSound("Weapons/Sawedoff/Fire", CHAN_WEAPON, CHANF_OVERLAP);
			A_FireBullets(7, 4, 8, 6, "BulletPuff");
			A_FireCustomMissile("GunSmokeSpawner",0,0,0,8);
			// This TakeInventory needs to stay here so altfire won't have
			// infinite ammo if there's a shell missing.
			A_TakeInventory("DoubleShotgunLoad", 1, TIF_NOTAKEINFINITE);
		}
		SG2F C 1 Offset(1,42) A_AlertMonsters
		SG2F D 1 Offset(2,45) A_SetPitch(pitch - 2.0)
		SG2F E 1 Offset(2,44) A_SetPitch(pitch - 1.5)
		SG2F F 1 Offset(1,39) A_SetPitch(pitch - 1.0)
		SG2F G 1 Offset(1,34) A_SetPitch(pitch - 1.0)
		SG2F H 1 Offset(0,32) A_SetPitch(pitch + 0.5)
		TNT1 A 0 A_CheckReload
		Goto Reload
	FireRight:
		SHTA A 1 BRIGHT {
			A_StartSound("Weapons/Sawedoff/Fire", CHAN_WEAPON, CHANF_OVERLAP);
			A_FireBullets(7, 4, 8, 6, "BulletPuff");
			A_FireCustomMissile("GunSmokeSpawner",0,0,0,8);
		}
		SG2F C 1 Offset(-1,42) A_AlertMonsters
		SG2F D 1 Offset(-2,45) A_SetPitch(pitch - 2.0)
		SG2F E 1 Offset(-2,44) A_SetPitch(pitch - 1.5)
		SG2F F 1 Offset(-1,39) A_SetPitch(pitch - 1.0)
		SG2F G 1 Offset(-1,34) A_SetPitch(pitch - 1.0)
		SG2F H 1 Offset(0,32) A_SetPitch(pitch + 0.5)
		Goto Ready2
	FireBoth:
		SG2F A 1 BRIGHT {
			A_PlaySound("Weapons/Sawedoff/DoubleFire", CHAN_WEAPON, CHANF_OVERLAP);
			A_TakeInventory("DoubleShotgunLoad",1,TIF_NOTAKEINFINITE);
			A_FireBullets(7, 4, 7, 6, "BulletPuff",0);
		}
		SG2F B 1 BRIGHT {
			A_TakeInventory("DoubleShotgunLoad",1,TIF_NOTAKEINFINITE);
			A_FireBullets(7, 4, 7, 6, "BulletPuff",0);
			A_FireBullets(7, 4, 2, 14, "BulletPuff", 0, 96);
			A_SpawnItemEx("GunSmoke", 50, -6, 32);
		}
		SG2F C 1 Offset(0,52) A_AlertMonsters
		SG2F D 1 Offset(0,55) A_SetPitch(pitch - 2.5)
		SG2F E 1 Offset(0,54) A_SetPitch(pitch - 2.0)
		SG2F F 1 Offset(0,49) A_SetPitch(pitch - 1.5)
		SG2F G 1 Offset(0,44) A_SetPitch(pitch - 1.0)
		SG2F H 1 Offset(0,38) A_SetPitch(pitch + 0.5)
		TNT1 A 0 A_CheckReload
		Goto Reload
	Altfire:
		SHT2 A 1 A_JumpIfInventory("DoubleShotgunLoad", 2, "FireBoth")
		Goto Fire
	Dryfire:
		SHT2 A 1
	Reload:
		SHT2 A 0 A_JumpIfInventory("DoubleShotgunLoad", 2, 2)
		SHT2 A 0 A_JumpIfInventory("NewShell", 1, "ProperReload")
		SHT2 A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready2
	ProperReload:
		SG2R AB 1
		SG2R C 1 A_PlaySound("Weapons/Sawedoff/Open", 5, CHANF_OVERLAP)
		SG2R DEFG 1
		SG2R H 1 { 
			A_StartSound("Weapons/Sawedoff/Out", 6, CHANF_OVERLAP);
			A_SpawnItemEx("ShotgunCasing", -8,-24,32, -2,random(-2,2),random(0,4), random(-55,-80), SXF_NOCHECKPOSITION);
			A_SpawnItemEx("ShotgunCasing", -8,-24,32, -2,random(-2,2),random(0,4), random(-55,-80), SXF_NOCHECKPOSITION);
		}
		SG2R IJKLMNOPQRS 1
		SG2R T 1 {
			if ( CountInv("DoubleShotgunLoad") == 0 && CountInv("NewShell") > 1 )
			{
				A_GiveInventory("DoubleShotgunLoad", 2);
				A_TakeInventory("NewShell", 2,TIF_NOTAKEINFINITE);
			}
			else
			{
				A_TakeInventory("NewShell", 1, TIF_NOTAKEINFINITE);
				A_GiveInventory("DoubleShotgunLoad", 1);
			}
			
			A_StartSound("Weapons/Sawedoff/In", 5, CHANF_OVERLAP);
		}
		SG2R UVWXYZ 1
		SG3R A 1 A_StartSound("Weapons/Sawedoff/Close", 6, CHANF_OVERLAP)
		SG3R BCDEFGHIJKL 1
		Goto Ready2
	}
}
