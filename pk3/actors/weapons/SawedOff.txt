ACTOR DoubleShotgunLoad : Ammo
{
	+IGNORESKILL
	Inventory.MaxAmount 2
	Inventory.Icon "SGN2A0"
}

ACTOR DoubleShotgun : DNWeapon replaces Shotgun
{
	Inventory.PickupMessage "You got the Maxwell One-Two sawed-off! (slot 3)"
	Inventory.PickupSound "Weapons/Sawedoff/Close"
	Weapon.SlotNumber 3
	Weapon.AmmoType "DoubleShotgunLoad"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "NewShell"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 2
	Weapon.Kickback 1000
	Weapon.SelectionOrder 5000
	+Weapon.AMMO_CHECKBOTH
	+Weapon.NoAlert
	+Weapon.NoAutoFire
	Tag "Maxwell Labs 1-2"
	States
	{
	Spawn:
		TNT1 A 0 Nodelay A_SetAngle(random(0,359))
		SGN2 A -1
		Stop
	Deselect:
		SHT2 A 1 Offset(-3,35)
		SHT2 A 1 Offset(-7,39)
		SHT2 B 1 Offset(-12,45)
		SHT2 C 1 Offset(-20,52)
		SHT2 D 1 Offset(-35,60)
		SHT2 E 1 Offset(-50,69)
		SHT2 F 1 Offset(-70,79)
		TNT1 A 5
		TNT1 A 0 A_Lower
		Wait
	Select:
	Ready:
		SHT2 F 1 Offset(-70,79) A_PlaySound("Weapons/Select3")
		SHT2 E 1 Offset(-50,69)
		SHT2 D 1 Offset(-35,60)
		SHT2 C 1 Offset(-20,52)
		SHT2 B 1 Offset(-12,45)
		SHT2 A 1 Offset(-7,39)
		SHT2 A 1 Offset(-3,35)
	Ready2:
		SHT2 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("DoubleShotgunLoad",1,1)
		Goto DryFire
		SHT2 A 0 A_JumpIfInventory("DoubleShotgunLoad", 2, "FireRight")
		SHTA B 0 A_PlaySound("Weapons/Sawedoff/Fire", CHAN_WEAPON)
		SHTA B 0 A_TakeInventory("DoubleShotgunLoad",1,TIF_NOTAKEINFINITE)
		SHTA B 1 BRIGHT A_FireBullets(7, 4, 8, 6, "BulletPuff",0)
		PISF A 0 A_FireCustomMissile("GunSmokeSpawner",0,0,0,8)
		SHT2 A 1 Offset(1,40) A_AlertMonsters
		SHT2 A 1 Offset(1,45) A_SetPitch(pitch - 2.0)
		SHT2 A 1 Offset(2,48) A_SetPitch(pitch - 1.5)
		SHT2 A 1 Offset(2,46) A_SetPitch(pitch - 1.0)
		SHT2 A 1 Offset(1,44) A_SetPitch(pitch - 0.5)
		SHT2 A 1 Offset(1,41) A_SetPitch(pitch + 0.5)
		TNT1 A 0 A_CheckReload
		Goto Ready2
	FireRight:
		SHTA B 0 A_PlaySound("Weapons/Sawedoff/Fire", 5)
		SHTA A 1 BRIGHT A_FireBullets(7, 4, 8, 6, "BulletPuff")
		PISF A 0 A_FireCustomMissile("GunSmokeSpawner",0,0,0,8)
		SHT2 A 1 Offset(-1,40) A_AlertMonsters
		SHT2 A 1 Offset(-1,45) A_SetPitch(pitch - 2.0)
		SHT2 A 1 Offset(-2,48) A_SetPitch(pitch - 1.5)
		SHT2 A 0 A_WeaponReady(WRF_NOBOB)
		SHT2 A 1 Offset(-2,46) A_SetPitch(pitch - 1.0)
		SHT2 A 0 A_WeaponReady(WRF_NOBOB)
		SHT2 A 1 Offset(-1,44) A_SetPitch(pitch - 0.5)
		SHT2 A 0 A_WeaponReady(WRF_NOBOB)
		SHT2 A 1 Offset(-1,41) A_SetPitch(pitch + 0.5)
		Goto Ready2
	FireBoth:
		SHTA B 0 A_PlaySound("Weapons/Sawedoff/DoubleFire", CHAN_WEAPON)
		SHO9 A 0 A_TakeInventory("DoubleShotgunLoad",1,TIF_NOTAKEINFINITE)
		SHO9 A 1 BRIGHT A_FireBullets(7, 4, 7, 6, "BulletPuff",0)
		SHO9 B 0 A_TakeInventory("DoubleShotgunLoad",1,TIF_NOTAKEINFINITE)
		SHO9 B 1 BRIGHT A_FireBullets(7, 4, 7, 6, "BulletPuff",0)
		SHO9 B 0 BRIGHT A_FireBullets(7, 4, 2, 14, "BulletPuff", 0, 96)
		PISF A 0 A_SpawnItemEx("GunSmoke", 50, -6, 32)
		SHT2 A 1 Offset(0,52) A_AlertMonsters
		SHT2 A 1 Offset(0,55) A_SetPitch(pitch - 2.5)
		SHT2 A 1 Offset(0,54) A_SetPitch(pitch - 2.0)
		SHT2 A 1 Offset(0,49) A_SetPitch(pitch - 1.5)
		SHT2 A 1 Offset(0,44) A_SetPitch(pitch - 1.0)
		SHT2 A 1 Offset(0,38) A_SetPitch(pitch + 0.5)
		TNT1 A 0 A_CheckReload
		Goto Reload
	Altfire:
		SHT2 A 1 A_JumpIfInventory("DoubleShotgunLoad", 2, "FireBoth")
		Goto Fire
	Dryfire:
		SHT2 A 1
	Reload:
		SHT2 A 0 A_JumpIfInventory("DoubleShotgunLoad", 2, 2)
		SHT2 A 0 A_JumpIfInventory("NewShell", 1, "ProperReload")
		SHT2 A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready2
	ProperReload:
		SHT2 ABCDEFF 1
		SHT2 G 1 A_PlaySound("Weapons/Sawedoff/Open")
		SHT2 GH 1
		SHT2 H 1 A_PlaySound("Weapons/Sawedoff/Out", 5)
		SHT2 IJK 1
		TNT1 A 0 A_JumpIfInventory("DoubleShotgunLoad", 1, "ReloadOneShell")
		SHT2 L 1 Offset(1,33) A_SpawnItemEx("ShotgunCasing", -8,-24,32, -2,random(-2,2),random(0,4), random(-55,-80), SXF_NOCHECKPOSITION)
		SHT2 L 1 Offset(2,34) A_SpawnItemEx("ShotgunCasing", -8,-24,32, -2,random(-2,2),random(0,4), random(-55,-80), SXF_NOCHECKPOSITION)
		SHT2 L 1 Offset(3,35) A_Jump(128,2)
		SHT2 L 1 Offset(4,36)
		SHT2 L 1 Offset(3,35) A_Jump(128,2)
		SHT2 L 1 Offset(2,34)
		SHT2 L 1 Offset(1,33)
		SHT2 MNOP 1
		TNT1 A 0 A_JumpIfInventory("NewShell", 2, "ReloadTwoShells")
		Goto ReloadOneShellWork
	ReloadTwoShells:
		TNT1 A 0 A_GiveInventory("DoubleShotgunLoad", 2)
		TNT1 A 0 A_PlaySound("Weapons/Sawedoff/In", 6)
		TNT1 A 0 A_TakeInventory("NewShell", 2,TIF_NOTAKEINFINITE)
		Goto ReloadContinue
	ReloadOneShell:
		SHT7 L 1 Offset(1,33) A_SpawnItemEx("ShotgunCasing", -8,-24,32, -2,random(-2,2),random(0,4), random(-55,-80), SXF_NOCHECKPOSITION)
		SHT7 L 1 Offset(2,34) A_Jump(128,2)
		SHT7 L 1 Offset(3,35)
		SHT7 L 1 Offset(4,36) A_Jump(128,2)
		SHT7 L 1 Offset(3,35)
		SHT7 L 1 Offset(2,34)
		SHT7 L 1 Offset(1,33)
		SHT7 MNOP 1
	ReloadOneShellWork:
		TNT1 A 0 A_GiveInventory("DoubleShotgunLoad", 1)
		TNT1 A 0 A_PlaySound("Shotload1", 6)
		TNT1 A 0 A_TakeInventory("NewShell", 1,TIF_NOTAKEINFINITE)
		Goto ReloadContinue
	ReloadContinue:
		SHT2 Q 1 Offset(2,33)
		SHT2 Q 1 Offset(4,34) A_Jump(128,2)
		SHT2 Q 1 Offset(6,35)
		SHT2 RSTUVWXY 1
		SHT2 Z 1 A_PlaySound("Weapons/Sawedoff/Close", 5)
		SHT2 FEDCBA 1
		Goto Ready2
	}
}